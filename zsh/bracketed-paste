# Code from Mikael Magnusson: http://www.zsh.org/mla/users/2011/msg00367.html

# create a new keymap to use while pasting
bindkey -N paste

# make everything in this keymap call our custom widget
bindkey -R -M paste "^@"-"\M-^?" paste-insert

# these are the codes sent around the pasted text in bracketed
# paste mode.
# do the first one with both -M viins and -M vicmd in vi mode
bindkey '^[[200~' _start_paste
bindkey -M paste '^[[201~' _end_paste

# insert newlines rather than carriage returns when pasting newlines
bindkey -M paste -s '^M' '^J'

zle -N _start_paste
zle -N _end_paste
zle -N zle-line-init _zle_line_init
zle -N zle-line-finish _zle_line_finish
zle -N paste-insert _paste_insert

# switch the active keymap to paste mode
function _start_paste() {
  bindkey -A paste main
}

# Go back to our normal keymap, & insert the pasted text. this has the nice
# effect of making the whole paste be a single undo/redo event.
function _end_paste() {
  set -x
  # Use bindkey -v here with vi mode probably. Maybe you want to track if you
  # were in ins or cmd mode and restore the right one.
  bindkey -rpM viins '\e'
  bindkey -v

  function zle-line-init { zle -K vicmd; }
  zle -N zle-line-init

  LBUFFER+=$_paste_content
  unset _paste_content
  set +x
}

function _paste_insert() {
  _paste_content+=$KEYS
}

function _zle_line_init() {
  # Tell terminal to send escape codes around pastes.
  [[ $TERM == rxvt-unicode-256color || $TERM == rxvt-unicode || $TERM == xterm || $TERM = xterm-256color || $TERM = screen || $TERM = screen-256color ]] && printf '\e[?2004h'
}

function _zle_line_finish() {
  # Tell it to stop when we leave zle, so pasting in other programs
  # doesn't get the ^[[200~ codes around the pasted text.
  [[ $TERM == rxvt-unicode-256color || $TERM == rxvt-unicode || $TERM == xterm || $TERM = xterm-256color || $TERM = screen || $TERM = screen-256color ]] && printf '\e[?2004l'
}

# vim:ft=zsh
