function set_git_root {
  local _git_root=$(gr)
  [ -z $_git_root ] || git_root=$_git_root && t=$_git_root
}

function set_default_database {
  unset PGDATABASE; db=$(current_db); [ -z $db ] || export PGDATABASE=$db
}

function set_ruby_version {
  if whence ruby > /dev/null; then
    ruby_version=" {$(ruby -v | awk '{print $2}')}"
  else
    ruby_version=""
  fi
}

function set_title {
  title "$(basename $PWD)"
}

setopt prompt_subst
autoload -Uz vcs_info

zstyle ':vcs_info:*' formats ' [%b]'
zstyle ':vcs_info:*' actionformats ' [%b|%F{1}%a%f]'
zstyle ':vcs_info:*' enable git

zle_highlight=(none)

function preexec {
  s=${@[2]}
  title "$s"
}

function precmd {
  vcs_info
  set_title
  set_git_root
  set_ruby_version
  set_default_database
}

# preexec_functions+=("set_title")

autoload colors zsh/terminfo

[[ "$terminfo[colors]" -gt 8 ]] && colors

for colour in red green yellow green cyan magenta white black; do
  eval pr_$colour='%{$fg[${colour}]%}'
done

pr_reset="%{$reset_color%}"

function set_prompt() {
  local lp="%m:%/${pr_cyan}${vcs_info_msg_0_}${pr_reset}"
  print "$lp"
}

function set_rprompt() {
  local rp="[%D{%H:%M:%S}]"

  if online; then
    print "${pr_cyan}${rp}${pr_reset}"
  else
    print "${pr_red}${rp}${pr_reset}"
  fi
}

PROMPT=$'$(set_prompt) '
RPROMPT=$'$(set_rprompt)'

# vim:ft=zsh
